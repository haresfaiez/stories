def resources_location = 'src/functionaltest/resources'
def source_location    = 'src/functionaltest/groovy'

configurations {
    functionaltestCompile.extendsFrom testCompile
    functionaltestRuntime.extendsFrom testRuntime
}

sourceSets {
    functionaltest {
        resources.srcDir file(resources_location)
        compileClasspath = sourceSets.main.output + configurations.functionaltestCompile
        runtimeClasspath = output + compileClasspath + configurations.functionaltestRuntime
    }
}

def cucumber_implementation_location = source_location
def cucumber_stories_location        = resources_location
task functionaltest() {
    dependsOn assemble
    doLast {
        javaexec {
            main      = "cucumber.api.cli.Main"
            classpath = sourceSets.functionaltest.runtimeClasspath
            args      = ['--plugin',
                         'pretty',
                         '--glue',
                         cucumber_implementation_location,
                         cucumber_stories_location]
        }
    }
}
check.dependsOn functionaltest

def cucumber_version                 = '1.2.4'
dependencies {
    testCompile group:   'info.cukes',
                name:    'cucumber-groovy',
                version: cucumber_version
    testCompile group:   'info.cukes',
                name:    'cucumber-junit',
                version: cucumber_version
}
