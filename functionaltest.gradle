ext {
    cucumber_package          = 'stories'
    cucumber_stories_location = 'src/functionaltest/resources'
    cucumber_version          = '1.2.4'
}

configurations {
    functionaltestCompile.extendsFrom testCompile
    functionaltestRuntime.extendsFrom testRuntime
}

sourceSets {
    functionaltest {
        java.srcDir file('src/functionaltest/java')
        resources.srcDir file('src/functionaltest/resources')
        compileClasspath = sourceSets.main.output + configurations.functionaltestCompile
        runtimeClasspath = output + configurations.functionaltestRuntime
    }
}

task functionaltest() {
    doLast {
        javaexec {
            main      = "cucumber.api.cli.Main"
            classpath = sourceSets.functionaltest.runtimeClasspath
            args      = ['--plugin',
                         'pretty',
                         '--glue',
                         cucumber_package,
                         cucumber_stories_location]
        }
    }
}

dependencies {
    testCompile group:   'info.cukes',
                name:    'cucumber-java',
                version: cucumber_version
    testCompile group:   'info.cukes',
                name:    'cucumber-junit',
                version: cucumber_version
}
